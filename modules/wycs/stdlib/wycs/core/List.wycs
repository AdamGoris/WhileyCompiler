package wycs.core

import wycs.core.Map

// Provides a rudimentary implemenetation of a "list"; that is a map
// from integer keys number consequtively from zero to values.

// Define the notion of a list
define List<V> ({int,V} pairs) where 
       forall [ ; p1 in pairs, p2 in pairs : p1[0] == p2[0] ==> p1[1] == p2[1] ] && // map invariant
       forall [ ; p in pairs : p[0] ≥ 0 && p[0] < |pairs| ]

function Append<V>({int,V} l1, {int,V} l2) => ({int,V} l3) where
       l1 ⊆ l3 && 
       forall [ ; p in l2 : (p[0]+|l1|,p[1]) in l3 ] && 
       forall [ ; p in l3 : (p[0] < |l1| && p in l1) || (p[0] >= |l1| && (p[0]-|l1|,p[1]) in l2) ] &&
       |l3| == |l1| + |l2|

function ListUpdate<V> ({int,V} pairs, int index, V value) => ({int,V} rs)
	 where (index,value) in rs && 
	        forall [ ; p1 in pairs, p2 in pairs : p1[0] == p2[0] ==> p1[1] == p2[1] ] && // map invariant
		forall [ ; p in pairs : p[0] ≥ 0 && p[0] < |pairs| ] && // list invariant
		forall [ ; p in pairs : p[0] == index || p in rs ] && 
		forall [ ; p in rs : p[0] == index || p in pairs]
