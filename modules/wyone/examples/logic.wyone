// ====================================================================
// Bool
// ====================================================================

term True
term False
class Bool as True | False

// ====================================================================
// Not
// ====================================================================

term Not(Term)

reduce Not(Bool b):
    => False, if b == True
    => True

reduce Not(Not(* x)):
    => x

reduce Not(And{Term... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

reduce Not(Or{Term... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{Term...}

reduce And{}:
    => True

reduce And{Term x}:
    => x

reduce And{Bool b, Term... xs}:
    => False, if b == False
    => And (xs)

reduce And{And{Term... xs}, Term... ys}:
    => And (xs ++ ys)

reduce And{Not(Term x), Term y, Term... ys}:
    => False, if x == y

reduce And{Or{Term... xs}, Term... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{Term...}

reduce Or{}:
    => False

reduce Or{Term x}:
    => x

reduce Or{Bool b, Term... xs}:
    => True, if b == True
    => Or (xs)

reduce Or{Not(Term x), Term y, Term... ys}:
    => True, if x == y

reduce Or{Or{Term... xs}, Term... ys}:
    => Or (xs ++ ys)

// ====================================================================
// Term
// ====================================================================

term Var(string)

class Term as Bool | Not | And | Or | Var 






