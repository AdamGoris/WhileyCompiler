include "logic.wyone"
include "numerics.wyone"
include "quantification.wyone"

// ====================================================================
// Set Expressions
// ====================================================================

term Set{Expr...}

// ====================================================================
// LengthOf
// ====================================================================
term LengthOf(Expr)

reduce LengthOf(Set{Value... xs}):
    => Num((real) |xs|)

reduce And{Equals[Num(int x), LengthOf(Expr v)], BExpr... rest}:
    => And (rest ++ Equals[v,Set{}]), if x == 0

reduce And{Equals[LengthOf(Expr v), Num(int x)], BExpr... rest}:
    => And (rest ++ Equals[v,Set{}]), if x == 0

// ====================================================================
// SubsetEq
// ====================================================================

term SubsetEq[SExpr,Expr]

reduce SubsetEq[Set{Expr... xs} s1, Set{Expr... ys} s2]:
    => True, if |xs| <= |ys| && no { x in xs | !(x in ys)}
    => False, if s1 is Set{Value...} && s2 is Set{Value...}

reduce SubsetEq[SExpr x, SExpr y]:
    => True, if x == y
    => True, if x is Set && |*x| == 0
    => Equals[x,y], if y is Set && |*y| == 0

// this rule could get very expensive?
reduce SubsetEq[Set{Expr x, Expr... xs} s1, Set{Expr... ys} s2]:
    => let ys = { Equals[x,y] | y in ys }
       in And {Or(ys),SubsetEq[Set(xs),s2]}, if !(x is Value)

reduce And{SubsetEq[Set{Expr... xs}, * x],SubsetEq[Set{Expr... ys}, SExpr y], BExpr... rest}:
    => let zs = (xs ++ ys) 
       in And (SubsetEq[Set(zs),x] ++ rest), if x == y

reduce And{SubsetEq[SExpr x, Set{Expr... xs}],SubsetEq[SExpr y, Set{Expr... ys}], BExpr... rest}:
    => let zs = { z | z in xs, true && z in ys }
       in And (SubsetEq[x,Set(zs)] ++ rest), if x == y

infer And{SubsetEq[SExpr x, SExpr y] s1,SubsetEq[SExpr u, SExpr v] s2, BExpr... rest}:
    => And ({s1,s2,SubsetEq[x,v]} ++ rest), if y == u

// ====================================================================
// Union
// ====================================================================

term Union{SExpr...}

reduce Union{Set{Expr... xs},Set{Expr... ys}, Expr... rest}:
    => Union (Set(xs++ys) ++ rest)

reduce Union{Expr e}:
    => e

// reduce ElementOf[Expr e1, Union{SExpr... xs}]:
//     => let ys = { ElementOf[e1,x] | x in xs }
//        in Or(ys)

infer Equals[LengthOf(Union{SExpr... xs}) l,AExpr e]:
       => let ieqs = { LessThanEq[LengthOf(x),e] | x in xs }
          in And({Equals[l,e]}++ieqs)
       	   
// ====================================================================
// Intersect
// ====================================================================

term Intersect{SExpr...}

reduce Intersect{Set{Expr... xs},Set{Expr... ys}, Expr... rest}:
     => let zs = { x | x in xs, true && x in ys }
        in Set(zs)

reduce Intersect{Expr e}:
    => e

// reduce ElementOf[Expr e1, Intersect{SExpr... xs}]:
//     => let ys = { ElementOf[e1,x] | x in xs }
//        in And(ys)

// ====================================================================
// Difference
// ====================================================================

term Difference[SExpr,SExpr]

reduce Difference[Set{Expr... xs},Set{Expr... ys}]:
     => let zs = { x | x in xs, !(x in ys) }
        in Set(zs)

// ====================================================================
// For All
// ====================================================================

reduce ForAll[QVar qv, Set{Expr... src}, BExpr be]:
       => And { be[qv \ s] | s in src }, if |src| > 0
       => True

infer And{ForAll[QVar qv, VExpr v1, BExpr qe] b1, 
           SubsetEq[Set{Expr... es}, Expr v3] b2, 
	   BExpr... bs}:
       => let qs = { qe[qv \ e] | e in es }
          in And ({b1,b2} ++ qs ++ bs), if v1 == v3

// ====================================================================
// Exists
// ====================================================================

reduce Exists[QVar qv, Set{Value... src}, BExpr be]:
       => Or { be[qv \ s] | s in src }, if |src| > 0
       => False

// ====================================================================
// Expressions & Values
// ====================================================================

define Value as Set{Value...} | ...

define SExpr as VExpr | Set | Union | ...

define BExpr as SubsetEq | ...

define VExpr as LengthOf | ...

define Expr as SExpr | ...
