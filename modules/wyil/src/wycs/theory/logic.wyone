include "variables.wyone"

// ====================================================================
// Bool
// ====================================================================

term True
term False
define Bool as True | False

// ====================================================================
// Not
// ====================================================================

term Not(BExpr)

reduce Not(Bool b):
    => False, if b == True
    => True

reduce Not(Not(* x)):
    => x

reduce Not(And{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

reduce Not(Or{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

reduce And{Atom x}:
    => x

reduce And{Bool b, Atom... xs}:
    => False, if b == False
    => True, if |xs| == 0
    => And (xs)

reduce And{And{BExpr... xs}, BExpr... ys}:
    => And (xs ++ ys)

reduce And{Not(Atom x), Atom y, Atom... ys}:
    => False, if x == y

reduce And{Or{BExpr... xs}, BExpr... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

reduce Or{Atom x}:
    => x

reduce Or{Bool b, Atom... xs}:
    => True, if b == True
    => False, if |xs| == 0
    => Or (xs)

reduce Or{Not(BExpr x), Atom y, Atom... ys}:
    => True, if x == y

reduce Or{Or{BExpr... xs}, BExpr... ys}:
    => Or (xs ++ ys)

// ====================================================================
// Expressions & Values
// ====================================================================

define Value as Bool | ...
define Atom as Bool | Not | ... // Not(Atom)
define BExpr as Atom | And | Or | Not
define Expr as Value | Atom | ...






