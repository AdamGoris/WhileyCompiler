package wyil.util

term Var(string)

class Expr as BExpr | AExpr | SExpr

// ====================================================================
// Boolean Expressions
// ====================================================================
term True
term False
class Bool as True | False

class BExpr as Bool 
	| Not 
	| And 
	| Or 
	| Var
	| Equals
	| NotEquals 
	| LessThan
	| LessThanEq 
	| GreaterThan
	| GreaterThanEq
	| SubSet
	| SubSetEq 
	| SupSet
	| SupSetEq
	 
// ====================================================================
// Arithmetic Expressions
// ====================================================================
class AExpr as Num | Sum | Mul | Var

term Num(int)
term Neg(AExpr)
term Sum{|AExpr...|}
term Mul{|AExpr...|}
term Div[AExpr,AExpr]

rewrite Equals{|Num(int x), Num(int y)|}:
    => True, if x == y
    => False
    
rewrite NotEquals{|Num(int x), Num(int y)|}:
    => True, if x != y
    => False

rewrite LessThan[Num(int x), Num(int y)]:
    => True, if x < y
    => False

rewrite LessThanEq[Num(int x), Num(int y)]:
    => True, if x <= y
    => False
    
rewrite GreaterThan[Num(int x), Num(int y)]:
    => True, if x > y
    => False

rewrite GreaterThanEq[Num(int x), Num(int y)]:
    => True, if x >= y
    => False

// ====================================================================
// Collections
// ====================================================================
class SExpr as Set | Var
class LExpr as List | Var
class CExpr as Set | List | Var

term Set{Expr...}
term ElementOf[Expr,SExpr]

term List[Expr...]
term IndexOf[LExpr, Expr]

term Tuple[Expr...]


term LengthOf[CExpr]

// ====================================================================
// Arithmetic Inequalities
// ====================================================================
term LessThan[AExpr,AExpr]
term LessThanEq[AExpr,AExpr]
term GreaterThan[AExpr,AExpr]
term GreaterThanEq[AExpr,AExpr]

rewrite GreaterThan[AExpr x, AExpr y]:
    => LessThan[y,x]

rewrite GreaterThanEq[AExpr x, AExpr y]:
    => LessThanEq[y,x]
    
// ====================================================================
// Equalities
// ====================================================================
term Equals{|Expr,Expr|}
term NotEquals{|Expr,Expr|}

rewrite Not(NotEquals (* x)):
    => Equals(x)
    
rewrite Not(Equals (* x)):
    => NotEquals(x)

rewrite And{Equals{|* x, * y|},Equals{|* u,* v|}, BExpr... rest}:
    => And ({Equals{|x,y|},Equals{|u,v|},Equals{|x,v|}} ++ rest), if y == u

rewrite And{NotEquals{|* x, * y|},Equals{|* u,* v|}, BExpr... rest}:
    => And ({NotEquals{|x,y|},Equals{|u,v|},NotEquals{|x,v|}} ++ rest), if y == u

rewrite And{Equals{|* x, * y|},LessThan[* u,* v], BExpr... rest}:
    => And ({Equals{|x,y|},LessThan[u,v],LessThan[x,v]} ++ rest), if y == u
    => And ({Equals{|x,y|},LessThan[u,v],LessThan[u,x]} ++ rest), if y == v

rewrite And{Equals{|* x, * y|},LessThanEq[* u,* v], BExpr... rest}:
    => And ({Equals{|x,y|},LessThanEq[u,v],LessThanEq[x,v]} ++ rest), if y == u
    => And ({Equals{|x,y|},LessThanEq[u,v],LessThanEq[u,x]} ++ rest), if y == v

// ====================================================================
// Set Constraints
// ====================================================================
term SubSet[SExpr,SExpr]
term SubSetEq[SExpr,SExpr]
term SupSet[SExpr,SExpr]
term SupSetEq[SExpr,SExpr]

// ====================================================================
// Not
// ====================================================================
term Not(BExpr)

rewrite Not(True):
    => False

rewrite Not(False):
    => True

rewrite Not(Not(* x)):
    => x

rewrite Not(And{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

rewrite Not(Or{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

rewrite And{True x}:
    => True

rewrite And{BExpr x}:
    => x

rewrite And{False, BExpr... xs}:
    => False

rewrite And{And{BExpr... xs}, BExpr... ys}:
    => And (xs ++ ys)

rewrite And{Not(BExpr x), BExpr y, BExpr... ys}:
    => False, if x == y

rewrite And{Or{BExpr... xs}, BExpr... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

rewrite Or{False}:
    => False

rewrite Or{BExpr x}:
    => x

rewrite Or{True, BExpr... xs}:
    => True

rewrite Or{Not(BExpr x), BExpr y, BExpr... ys}:
    => True, if x == y

rewrite Or{Or{BExpr... xs}, BExpr... ys}:
    => Or (xs ++ ys)








