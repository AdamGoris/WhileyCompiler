include "variables.wyone"

// ====================================================================
// Bool
// ====================================================================

term True
term False
class Bool as True | False

// ====================================================================
// Not
// ====================================================================

term Not(BExpr)

reduce Not(Bool b):
    => False, if b == True
    => True

reduce Not(Not(* x)):
    => x

reduce Not(And{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in Or(ys)

reduce Not(Or{BExpr... xs}):
    => let ys = { Not(x) | x in xs }
       in And(ys)

// ====================================================================
// And
// ====================================================================

term And{BExpr...}

reduce And{BExpr x}:
    => x

reduce And{Bool b, BExpr... xs}:
    => False, if b == False
    => True, if |xs| == 0
    => And (xs)

reduce And{And{BExpr... xs}, BExpr... ys}:
    => And (xs ++ ys)

reduce And{Not(BExpr x), BExpr y, BExpr... ys}:
    => False, if x == y

reduce And{Or{BExpr... xs}, BExpr... ys}:
    => let ys = { And(x ++ ys) | x in xs }
       in Or(ys)

// ====================================================================
// Or
// ====================================================================

term Or{BExpr...}

reduce Or{BExpr x}:
    => x

reduce Or{Bool b, BExpr... xs}:
    => True, if b == True
    => False, if |xs| == 0
    => Or (xs)

reduce Or{Not(BExpr x), BExpr y, BExpr... ys}:
    => True, if x == y

reduce Or{Or{BExpr... xs}, BExpr... ys}:
    => Or (xs ++ ys)

// ====================================================================
// Congruence
// ====================================================================

term Equals{|Expr,Expr|}

reduce Equals{|Expr x, Expr y|}:
    => True, if x == y

reduce Equals{|Value x, Value y|}:
    => False, if x != y

infer And{Equals{|Var(string s) v,Expr e|} eq, BExpr... xs}:
    => let ys = { x[v\e] | x in xs}
       in And (eq ++ ys)

// IS THIS RULE NECESSARY?
// infer And{Equals{|Expr x, Expr y|} e1, Equals{|Expr u, Expr v|} e2, BExpr... rest}:
//     => let e3 = Equals{|x,v|}
//        in And ({e1,e2,e3} ++ rest), if y == u && x != v

// ====================================================================
// Expressions & Values
// ====================================================================

class Value as Bool | ...
class BExpr as Bool | Var | Not | And | Or | Equals | ...
class Expr as Bool | Var | Not | And | Or | Equals | ...






