include "logic.wyone"
include "numerics.wyone"

// ====================================================================
// LessThan
// ====================================================================

term LessThan[AExpr,AExpr] // syntactic sugar

reduce LessThan[AExpr x, AExpr y]:
    => let negx = Mul[-1.0,{|x|}]
       in ZeroLessThan (Sum[0.0,{|y,negx|}])

// ====================================================================
// Inequality
// ====================================================================

term ZeroLessThan(AExpr) // 0 < e

reduce ZeroLessThan(Num(real x)):
    => True, if 0.0 < x
    => False

reduce Not(ZeroLessThan(AExpr x)):
    => let negx = Sum[0.0,{|Mul[-1.0,{|x|}]|}]
       in Or{Equals{|x,Num(0.0)|},ZeroLessThan(negx)}

// rearrange linear inequality
// NOTE: must be inference rule to ensure Sums are maximally reduced
// NOTE: can't do anything useful with non-variable terms :(
// NOTE: requirement x2 < 0.0 && y2 > 0.0 ensures we are eliminating variables
infer And{ZeroLessThan(Sum[real x1, {|Mul[real x2, {|Var v1|}], Expr... xs|}] s1) ieq1, 
          ZeroLessThan(Sum[real y1, {|Mul[real y2, {|Var v2|}], Expr... ys|}] s2) ieq2, BExpr... rest}:
     => let ieq3 = ZeroLessThan(Sum[0.0,{|s1,s2|}])
        in And ({ieq1,ieq2,ieq3} ++ rest), if v1 == v2 && x2 < 0.0 && y2 > 0.0

// ====================================================================
// Expressions & Values
// ====================================================================

class BExpr as ZeroLessThan | ...
class Expr as ZeroLessThan | ...
