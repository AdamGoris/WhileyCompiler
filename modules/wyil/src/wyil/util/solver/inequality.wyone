include "logic.wyone"
include "numerics.wyone"

// ====================================================================
// Inequalities
// ====================================================================

term LessThan[AExpr,AExpr]

reduce LessThan[Num(int x), Num(int y)]:
    => True, if x < y
    => False

reduce LessThan[Var x, Var y]:
    => False, if x == y

reduce Not(LessThan[* x, * y]):
    => Or{Equals{x,y},LessThan[y,x]}

// the following rule is broken because we need to eliminate a
// variable, or it won't terminate!
infer And{LessThan[Num(int i) n, Sum{|AExpr... xs|}] ieq1, LessThan[Num(int j), Sum{|AExpr... ys|}] ieq2, BExpr... rest}:
    => let ieq3 = LessThan[n,Sum (xs ++ ys)]
       in And ({ieq1,ieq2,ieq3} ++ rest), if i == 0 && j == 0 && some { x in xs | x is Var && x in ys }

// ====================================================================
// Rearrangement
// ====================================================================

reduce LessThan[AExpr x, AExpr y]:
    => let z = Sum{|Mul{|Num(-1),x|},y|}
       in LessThan[Num(0),z], if !(x is Num(int) && *x == 0)

// ====================================================================
// Expressions & Values
// ====================================================================

class BExpr as LessThan | ...
class Expr as LessThan | ...
